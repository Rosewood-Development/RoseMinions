import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'maven-publish'
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.encoding = 'UTF-8'
}

dependencies {
    // Other modules
    compileOnly project(':NMS:Wrapper')

    // Referenced dependencies
    compileOnly "io.papermc.paper:paper-api:$spigotVersion-R0.1-SNAPSHOT"
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'com.arcaniax:HeadDatabase-API:1.3.1', {
        exclude group: 'org.spigotmc'
    }

    // Mojang dependencies
    compileOnly 'com.mojang:authlib:1.5.21'

    // Shop Hook Dependencies
    compileOnly 'com.github.brcdev-minecraft:shopgui-api:3.0.0'
    compileOnly 'com.github.Gypopo:EconomyShopGUI-API:1.5.0'
    compileOnly 'net.essentialsx:EssentialsX:2.19.7'
    compileOnly 'com.zrips:cmi:9.0.1.3'
    // TODO: Add BossShopPro

    // Economy Hook Dependencies
    compileOnly 'com.github.milkbowl:vault:1.7.3'
    compileOnly 'me.lokka30:treasury-api:1.1.1'
    compileOnly 'org.black_ixx:playerpoints:3.1.0'
    compileOnly 'me.realized:tokenmanager:3.2.5'

    // Dependencies that will be shaded into the jar
    api 'dev.rosewood:rosegarden:1.2.5'
    api 'dev.rosewood:guiframework:1.1.3.1-DEV-SNAPSHOT'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

shadowJar {
    archiveClassifier.set(null)
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'roseminions'
            pom {
                name = 'roseminions'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}
