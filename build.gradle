plugins {
    id 'com.gradleup.shadow' version '8.3.5' apply false
    id 'maven-publish'
    id 'java'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.gradleup.shadow'
    group = 'dev.rosewood'
    version = '1.0.0-SNAPSHOT'

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(21))
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:24.1.0'
    }

    repositories {
        mavenCentral()

        maven { url = 'https://repo.rosewooddev.io/repository/public/' }
        maven { url = 'https://libraries.minecraft.net' }
        maven { url = 'https://jitpack.io/' }
        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url = 'https://repo.essentialsx.net/releases/' }
        maven { url = 'https://repo.codemc.org/repository/maven-public' }

        maven {
            url = 'https://repo.rosewooddev.io/repository/maven-public'
            credentials {
                username project.mavenUser
                password project.mavenPassword
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}

dependencies {
    implementation project(':Plugin')
    implementation project(':NMS:Wrapper')
    implementation project(path: ':NMS:v1_19_R3', configuration: 'remapped')
    implementation project(path: ':NMS:v1_20_R3', configuration: 'remapped')
    implementation project(path: ':NMS:v1_20_R4', configuration: 'remapped')
    implementation project(path: ':NMS:v1_21_R1', configuration: 'remapped')
    implementation project(path: ':NMS:v1_21_R2', configuration: 'remapped')
    implementation project(path: ':NMS:v1_21_R3', configuration: 'remapped')
    implementation project(path: ':NMS:v1_21_R4', configuration: 'remapped')
    implementation project(path: ':NMS:v1_21_R5', configuration: 'remapped')
}

shadowJar {
    archiveClassifier.set(null)

    relocate('dev.rosewood.rosegarden', 'dev.rosewood.roseminions.lib.rosegarden')
    relocate('dev.rosewood.guiframework', 'dev.rosewood.roseminions.lib.guiframework')

    exclude 'dev/rosewood/rosegarden/lib/hikaricp/**/*.class'
    exclude 'dev/rosewood/rosegarden/lib/slf4j/**/*.class'
}

build.dependsOn shadowJar
//generateMetadataFileForShadowPublication.dependsOn jar
