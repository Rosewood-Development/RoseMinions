import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'java-library'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
group = 'dev.rosewood'
version = '1.0.0-SNAPSHOT'

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.forkOptions.executable = 'javac'
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    maven { url = 'https://libraries.minecraft.net' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly 'org.jetbrains:annotations:23.0.0'

    api 'dev.rosewood:rosegarden:1.1.0.48-SNAPSHOT'
    api 'dev.rosewood:guiframework:1.1.3-SNAPSHOT'
}

shadowJar {
    archiveClassifier.set(null)
    minimize()

    relocate('dev.rosewood.rosegarden', 'dev.rosewood.roseminions.lib.rosegarden')
    relocate('dev.rosewood.guiframework', 'dev.rosewood.roseminions.lib.guiframework')

    exclude 'dev/rosewood/rosegarden/lib/hikaricp/**/*.class'
    exclude 'dev/rosewood/rosegarden/lib/slf4j/**/*.class'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'roseminions'
            pom {
                name = 'roseminions'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

build.dependsOn shadowJar
